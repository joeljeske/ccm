load("@build_bazel_rules_nodejs//:index.bzl", "generated_file_test", "nodejs_binary", "npm_package_bin")
load("@npm//@bazel/typescript:index.bzl", "ts_library")

package(default_testonly = True)

ts_library(
    name = "compile",
    srcs = glob(["*.ts"]),
    module_name = package_name(),
    deps = [
        "//packages/postcss-css-components:compile",
        "@npm//@types/node",
        "@npm//postcss",
    ],
)

nodejs_binary(
    name = "transform",
    data = [":compile"],
    entry_point = "transform.ts",
)

test_file_ext = ".ccm.css"

test_names = [
    (
        file,
        file[:-len(test_file_ext)],
    )
    for file in glob(["test*" + test_file_ext])
]

[
    (
        npm_package_bin(
            name = test + "-gen",
            outs = [test + ".gen.css"],
            args = [
                "$@",
                "$(location %s)" % file,
            ],
            data = [file],
            tool = ":transform",
        ),
        generated_file_test(
            name = test,
            src = test + ".golden.css",
            generated = test + ".gen.css",
        ),
    )
    for (file, test) in test_names
]
