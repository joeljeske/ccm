load("@build_bazel_rules_nodejs//:index.bzl", "npm_package_bin", "pkg_npm")
load("//tools/create-npm-json:index.bzl", "create_npm_json")
load("//:npm_substitutions.bzl", "npm_substitutions")
load("@npm//@bazel/typescript:index.bzl", "ts_library")
load("@npm//rollup:index.bzl", "rollup")

npm_package_bin(
    name = "templates",
    args = ["$(@D)"],
    output_dir = True,
    tool = "//packages/templates:copy",
)

ts_library(
    name = "compile",
    srcs = glob(["*.ts"]),
    module_name = package_name(),
    deps = [
        "//packages/node-api:compile",
        "@npm//@rollup/pluginutils",
        "@npm//@types/node",
        "@npm//rollup",
    ],
)

filegroup(
    name = "esm",
    srcs = [
        "compile",
        "//packages/node-api:compile",
        "//packages/postcss-css-components:compile",
    ],
    output_group = "es6_sources",
)

rollup(
    name = "bundle",
    outs = ["plugin.js"],
    args = [
        "$(RULEDIR)",
        "--file $@",
        "--format cjs",
        "--exports named",
        "--silent",
        "--plugin @rollup/plugin-node-resolve",
        "--plugin @rollup/plugin-commonjs",
        "--external rollup",
        "--external @rollup/pluginutils",
    ],
    data = [
        "esm",
        "@npm//@rollup/plugin-commonjs",
        "@npm//@rollup/plugin-node-resolve",
        "@npm//css-selector-tokenizer",
        "@npm//lodash.template",
        "@npm//postcss",
        "@npm//postcss-modules",
    ],
)

create_npm_json(name = "package-json")

pkg_npm(
    name = "npm",
    package_name = npm_substitutions[package_name()],
    substitutions = npm_substitutions,
    tags = ["npm_public"],
    visibility = ["//visibility:public"],
    deps = [
        "README.md",
        ":bundle",
        ":package.json",
        ":templates",
    ],
)
